if(CAOS_BUILD_EXAMPLES AND (CAOS_BUILD_PHP_BINDING OR CAOS_BUILD_NODE_BINDING OR CAOS_BUILD_PYTHON_BINDING OR CAOS_USE_CROWCPP))
  set(CAOS_EXAMPLE_QUERY "--caos-example-query")
endif()

if(EXISTS ${CMAKE_SOURCE_DIR}/cmake/type_code.cmake)
  include(${CMAKE_SOURCE_DIR}/cmake/type_code.cmake)
endif()

if(CAOS_TEMPLATE_CODE OR CAOS_CROWCPP_CODE)
  set(CAOS_TEMPLATE_QUERY "--caos-template-query")
endif()

find_package(Python3 REQUIRED)

# set(QUERY_DEFINITIONS_FILE "${CMAKE_SOURCE_DIR}/query_definitions.txt")
set(QUERY_DEFINITIONS_FILE "${CMAKE_SOURCE_DIR}/queries.yaml")
set(QUERY_GENERATOR_SCRIPT "${CMAKE_SOURCE_DIR}/bin/generate_queries.py")
set(GENERATED_QUERIES_DIR "${CMAKE_BINARY_DIR}/generated_queries")

if(NOT EXISTS "${QUERY_DEFINITIONS_FILE}")
  message(FATAL_ERROR "The query definition file was not found! Expected path: ${QUERY_DEFINITIONS_FILE}")
endif()

if(NOT EXISTS "${QUERY_GENERATOR_SCRIPT}")
  message(FATAL_ERROR "The query generator script was not found! Expected path: ${QUERY_GENERATOR_SCRIPT}")
endif()

file(MAKE_DIRECTORY "${GENERATED_QUERIES_DIR}")

set_property(DIRECTORY APPEND PROPERTY CMAKE_CONFIGURE_DEPENDS
  ${QUERY_DEFINITIONS_FILE}
  ${QUERY_GENERATOR_SCRIPT}
)

message(STATUS "Generating query interfaces from ${QUERY_DEFINITIONS_FILE}")

execute_process(
  COMMAND ${Python3_EXECUTABLE}
    ${QUERY_GENERATOR_SCRIPT}
    --definitions ${QUERY_DEFINITIONS_FILE}
    --output-dir ${GENERATED_QUERIES_DIR}
    ${CAOS_EXAMPLE_QUERY}
    ${CAOS_TEMPLATE_QUERY}
  WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
  RESULT_VARIABLE QUERY_GEN_RESULT
  OUTPUT_VARIABLE QUERY_GEN_OUTPUT
  ERROR_VARIABLE QUERY_GEN_ERROR
)

# Debug output
if(QUERY_GEN_OUTPUT)
  message(STATUS "Python output: ${QUERY_GEN_OUTPUT}")
endif()
if(QUERY_GEN_ERROR)
  message(WARNING "Python errors: ${QUERY_GEN_ERROR}")
endif()


if(NOT QUERY_GEN_RESULT EQUAL 0)
  message(FATAL_ERROR "Query generation failed!\nOutput: ${QUERY_GEN_OUTPUT}\nError: ${QUERY_GEN_ERROR}")
endif()

set(GENERATED_QUERY_DEFINITION "${GENERATED_QUERIES_DIR}/Query_Definition.hpp")
set(GENERATED_QUERY_OVERRIDE "${GENERATED_QUERIES_DIR}/Query_Override.hpp")
set(GENERATED_CACHE_FORWARDING "${GENERATED_QUERIES_DIR}/Cache_Query_Forwarding.hpp")
set(GENERATED_DATABASE_FORWARDING "${GENERATED_QUERIES_DIR}/Database_Query_Forwarding.hpp")
set(GENERATED_AUTH_CONFIG "${GENERATED_QUERIES_DIR}/AuthConfig.hpp")
set(GENERATED_QUERY_CONFIG "${GENERATED_QUERIES_DIR}/Query_Config.cmake")

include(${GENERATED_QUERY_CONFIG})

target_include_directories(${PROJECT_NAME} PRIVATE
  ${GENERATED_QUERIES_DIR}
)
