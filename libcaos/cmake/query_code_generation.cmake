find_package(Python3 REQUIRED)

set(QUERY_DEFINITIONS_FILE "${CMAKE_CURRENT_SOURCE_DIR}/query_definitions.txt")
if(NOT EXISTS "${QUERY_DEFINITIONS_FILE}")
    message(FATAL_ERROR "The query definition file was not found! Expected path: ${QUERY_DEFINITIONS_FILE}")
endif()

set(QUERY_GENERATOR_SCRIPT "${CMAKE_CURRENT_SOURCE_DIR}/generate_queries.py")
set(GENERATED_QUERIES_DIR "${CMAKE_BINARY_DIR}/generated_queries")

file(MAKE_DIRECTORY "${GENERATED_QUERIES_DIR}")

set(GENERATED_QUERY_DEFINITION "${GENERATED_QUERIES_DIR}/Query_Definition.hpp")
set(GENERATED_QUERY_OVERRIDE "${GENERATED_QUERIES_DIR}/Query_Override.hpp")
set(GENERATED_CACHE_FORWARDING "${GENERATED_QUERIES_DIR}/Cache_Query_Forwarding.hpp")
set(GENERATED_DATABASE_FORWARDING "${GENERATED_QUERIES_DIR}/Database_Query_Forwarding.hpp")

add_custom_command(
  OUTPUT
    ${GENERATED_QUERY_DEFINITION}
    ${GENERATED_QUERY_OVERRIDE}
    ${GENERATED_CACHE_FORWARDING}
    ${GENERATED_DATABASE_FORWARDING}
  COMMAND ${Python3_EXECUTABLE}
    ${QUERY_GENERATOR_SCRIPT}
    --definitions ${QUERY_DEFINITIONS_FILE}
    --output-dir ${GENERATED_QUERIES_DIR}
  DEPENDS ${QUERY_DEFINITIONS_FILE} ${QUERY_GENERATOR_SCRIPT}
  COMMENT "Generating query interfaces from ${QUERY_DEFINITIONS_FILE}"
  WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
)

add_custom_target(generate_queries ALL
  DEPENDS
    ${GENERATED_QUERY_DEFINITION}
    ${GENERATED_QUERY_OVERRIDE}
    ${GENERATED_CACHE_FORWARDING}
    ${GENERATED_DATABASE_FORWARDING}
)

add_dependencies(${PROJECT_NAME} generate_queries)

target_include_directories(${PROJECT_NAME} PUBLIC
    ${GENERATED_QUERIES_DIR}
)
